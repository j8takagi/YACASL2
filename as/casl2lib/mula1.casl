;;; -32767〜32767の範囲にある正数のかけ算（筆算方式）を行う
;;; 入力 GR1:被乗数 GR2:乗数
;;; 出力 GR0:積
;;; 被乗数または乗数が-32768の場合は、オーバーフロー
;;; 積が-32767未満または32767より大きい場合は、GR0は積の下位16ビットになり、オーバーフロー
MULA    START
        RPUSH
        XOR     GR0,GR0         ; 積
        AND     GR1,GR1         ; (GR1 = 0)の場合、終了
        JZE     FIN             ; ↓
        AND     GR2,GR2         ; (GR2 = 0)の場合、終了
        JZE     FIN             ; ↓
        LAD     GR3,1           ; 対象ビット
        XOR     GR4,GR4         ; マイナスフラグ
CHK1    AND     GR1,GR1         ; (GR1 > 0)の場合は、CHK2へジャンプ
        JPL     CHK2            ; ↓
        XOR     GR1,ALLON       ; GR1の正負を反転
        ADDA    GR1,ONE         ; ↓
        JOV     FIN             ; (GR1 = -32768)の場合は終了
        XOR     GR4,ONE         ; GR4 <- 1
CHK2    AND     GR2,GR2         ; (GR1 > 0)の場合は、LOOPへジャンプ
        JPL     LOOP            ; ↓
        XOR     GR2,ALLON       ; GR2の正負を反転
        ADDA    GR2,ONE         ; ↓
        JOV     FIN             ; (GR2 = -32768)の場合は終了
        XOR     GR4,ONE         ; マイナスフラグを反転
LOOP    PUSH    0,GR3           ; ループ先頭。GR2のビット中でGR3の示したものが0の場合、NEXTへジャンプ
        AND     GR3,GR2         ; ↓
        POP     GR3             ; ↓
        JZE     NEXT            ; ↓
        ADDA    GR0,GR1         ; GR0 <- GR0 + GR1
        JOV     FIN             ; オーバーフローした場合、終了
NEXT    SLA     GR3,1           ; GR3を1回左シフト
        AND     GR3,GR3         ; (GR3 = 0)の場合、ループ脱出
        JZE     MIN             ; ↓
        CPL     GR3,GR2         ; (GR3 > GR2)の場合、ループ脱出
        JPL     MIN             ; ↓
        SLA     GR1,1           ; GR1を1回左シフト
        JOV     FIN             ; オーバーフローした場合、終了
        JUMP    LOOP            ; ループ終端
MIN     AND     GR4,GR4         ; マイナスフラグがオフの場合、終了
        JZE     FIN             ; ↓
        XOR     GR0,ALLON       ; GR1の正負を反転
        ADDA    GR0,ONE         ; ↓
FIN     RPOP
        RET
ONE     DC      1
ALLON   DC      #FFFF
        END
