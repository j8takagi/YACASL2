;;; -32767〜32767の範囲にある正数のかけ算（筆算方式）を行う
;;; 入力 GR1:被乗数 GR2:乗数
;;; 出力 GR0:積
;;; 被乗数または乗数が-32768の場合は、オーバーフロー
;;; 積が-32767未満または32767より大きい場合は、GR0は積の下位16ビットになり、オーバーフロー
MULA    START
        RPUSH
        XOR     GR0,GR0         ; 積
        AND     GR1,GR1         ; (GR1 = 0)の場合、終了
        JZE     FIN             ; ↓
        AND     GR2,GR2         ; (GR2 = 0)の場合、終了
        JZE     FIN             ; ↓
        LAD     GR3,1           ; 対象ビット
        XOR     GR4,GR4         ; マイナスフラグ
CHK1    AND     GR1,GR1         ; (GR1 > 0)の場合は、CHK2へジャンプ
        JPL     CHK2            ; ↓
        XOR     GR1,ALLON       ; GR1の正負を反転
        ADDA    GR1,ONE         ; ↓
        JOV     FIN             ; (GR1 = -32768)の場合は終了
        XOR     GR4,ONE         ; GR4 <- 1
CHK2    AND     GR2,GR2         ; (GR1 > 0)の場合は、LOOPへジャンプ
        JPL     MUL             ; ↓
        XOR     GR2,ALLON       ; GR2の正負を反転
        ADDA    GR2,ONE         ; ↓
        JOV     FIN             ; (GR2 = -32768)の場合は終了
        XOR     GR4,ONE         ; マイナスフラグを反転
MUL     CALL    MULL            ; GR0 <- GR1 * GR2
        JOV     FIN
        AND     GR0,GR0
        JMI     OV
MIN     AND     GR4,GR4         ; マイナスフラグがオフの場合、終了
        JZE     FIN             ; ↓
        XOR     GR0,ALLON       ; GR1の正負を反転
        ADDA    GR0,ONE         ; ↓
        JUMP    FIN
OV      ADDL    GR0,=#8000
FIN     RPOP
        RET
ONE     DC      1
ALLON   DC      #FFFF
        END
;;; 0〜65535の範囲にある正数のかけ算（筆算方式）を行う
;;; 入力 GR1:被乗数 GR2:乗数
;;; 出力 GR0:積の下位WORD GR3:積の上位WORD
;;; 積が65535より大きい場合は、オーバーフロー
MULL    START
        PUSH    0,GR4
        PUSH    0,GR5
        XOR     GR0,GR0         ; 積
        XOR     GR3,GR3         ; 上位word
        XOR     GR5,GR5         ; 上位wordの一時値
        AND     GR1,GR1         ; (GR1 = 0)の場合、終了
        JZE     CHKOV           ; ↓
        AND     GR2,GR2         ; (GR2 = 0)の場合、終了
        JZE     CHKOV           ; ↓
        LAD     GR4,1           ; 対象ビット
LOOP    PUSH    0,GR4           ; ループ先頭。GR2のビット中でGR4が示すビットが0の場合、NEXTへジャンプ
        AND     GR4,GR2         ; ↓
        POP     GR4             ; ↓
        JZE     NEXT1           ; ↓
        ADDL    GR3,GR5         ; GR3 <- GR3 + GR5
        ADDL    GR0,GR1         ; GR0 <- GR0 + GR1
        JOV     AHB1            ; GR0がオーバーフローした場合、AHB1へジャンプ
        JUMP    NEXT1           ; ↓
AHB1    LAD     GR3,1,GR3       ; GR3 <- GR3 + 1
NEXT1   SLL     GR4,1           ; GR4を1回左シフト
        JOV     CHKOV           ; ↓
        SLL     GR5,1           ; GR5を1回左シフト
        CPL     GR4,GR2         ; (GR4 > GR2)の場合、CHKOVへジャンプ
        JPL     CHKOV           ; ↓
        SLL     GR1,1           ; GR1を1回左シフト
        JOV     AHB2            ; GR1がオーバーフローした場合、AHBへジャンプ
        JUMP    NEXT2           ; ↓
AHB2    LAD     GR5,1,GR5       ; GR5 <- GR5 + 1
NEXT2   JUMP    LOOP            ; ループ終端
CHKOV   AND     GR3,GR3         ; GR3 = 0の場合、終了
        JZE     FIN             ; ↓
        LAD     GR4,#FFFF       ; GR3 <> 0の場合、オーバーフロー
        SLL     GR4,1           ; ↓
FIN     POP     GR5
        POP     GR4
        RET
        END
